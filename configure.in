AC_INIT(kaffe)
AM_INIT_AUTOMAKE(kaffe, 1.0b3)

AM_MAINTAINER_MODE

dnl needed for autoheader to find acconfig.h
AUTOHEADER="$AUTOHEADER --localdir config"

dnl Setup version number.
KVER=$VERSION

dnl =========================================================================
dnl Allow specification of header-file and library directories
dnl -------------------------------------------------------------------------

AC_ARG_WITH(includes, [  --with-includes=dirs    Search specified directories for header files], CPPFLAGS="$CPPFLAGS"`echo " $withval" | sed 's/ / -I/g'`)
AC_ARG_WITH(libraries,[  --with-libraries=dirs   Search specified directories for libraries], LDFLAGS="$LDFLAGS"`echo " $withval" | sed 's/ / -L/g'`)

dnl Find the compiler early on in case we need to override.
AC_PROG_CC

dnl If gcc is the compiler, compile with -Wall for lots of warnings
if test "$GCC" = "yes"; then
	CFLAGS="$CFLAGS -Wall -Wstrict-prototypes"
fi

dnl Find the machine type and setup the relevant links
AC_CANONICAL_HOST
AC_EXEEXT

dnl =========================================================================
dnl By default we use dynamic libraries for everything
dnl -------------------------------------------------------------------------

dynamic_libraries=yes
vm_dynamic_library=yes

dnl -------------------------------------------------------------------------

dnl =========================================================================
dnl Force use of static libraries
dnl -------------------------------------------------------------------------

AC_ARG_WITH(staticlib,[  --with-staticlib        Force the use of static libraries rather than shared])
if test x"$with_staticlib" = x"yes" ; then
	dynamic_libraries=no
	if test x"${with_staticvm+set}" != x"set"; then
		with_staticvm=yes
	fi
fi

AC_ARG_WITH(staticvm,[  --with-staticvm         Force the creation of a static virtual machine])
if test x"$with_staticvm" = x"yes" ; then
	vm_dynamic_library=no
fi

dnl =========================================================================
dnl Look for the configuration information
dnl -------------------------------------------------------------------------

known=no
orig_host_cpu=$host_cpu
orig_host_os=$host_os
if test -f $srcdir/config/config.alias ; then
	. $srcdir/config/config.alias
fi
if test -f $srcdir/config/$host_cpu/$host_os/config.frag ; then
	known=yes
	. $srcdir/config/$host_cpu/$host_os/config.frag
fi
if test "$known" = "no" ; then
	AC_MSG_ERROR(Configuration $host_cpu-$host_os not supported)
fi

dnl =========================================================================

dnl =========================================================================
dnl Configure libtool
dnl -------------------------------------------------------------------------

case "$dynamic_libraries,$vm_dynamic_library" in
no,no) : ${enable_shared=no};;
yes,yes) : ${enable_static=no};;
# otherwise, we have to compile everything twice
esac

AM_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)

dnl =========================================================================
dnl Do we need underscore?
dnl -------------------------------------------------------------------------

if test x"$ac_cv_sys_symbol_underscore" = x"yes"; then
    AC_DEFINE(HAVE_UNDERSCORED_C_NAMES, 1, [Define if C symbol names start with underscore])
fi

dnl =========================================================================
dnl Start of default configurations
dnl -------------------------------------------------------------------------

dnl What kind of system are we using?
case $host_os in
win32*)
	SYSTEM=win32 ;;
*)
	SYSTEM=unix ;;
esac

dnl -------------------------------------------------------------------------
dnl End of default configurations
dnl =========================================================================

dnl =========================================================================
dnl This is a hack to ease the generation of forward-sources, i.e.,
dnl sources that just #include others, because automake can't use
dnl sources from other directories
dnl -------------------------------------------------------------------------

AC_SUBST(REGEN_FORWARD)
REGEN_FORWARD="\$(top_srcdir)/config/regen-forward"

dnl =========================================================================
dnl Set the base set of platform-dependent files 
dnl One-liners with #include directives will be created in the Makefiles
dnl -------------------------------------------------------------------------

CONFIG_MD_H="\$(srcdir)/$host_cpu/$host_os/md.h"
KAFFEVM_MD_C="\$(top_srcdir)/config/$host_cpu/$host_os/md.c"
AC_SUBST(CONFIG_MD_H)
AC_SUBST(KAFFEVM_MD_C)

dnl =========================================================================

dnl =========================================================================
dnl Look for configuration specific Makefile fragement and include it
dnl -------------------------------------------------------------------------

if test -f $srcdir/config/$host_cpu/$host_os/Make.frag ; then
	cpu_os_frag=$srcdir/config/$host_cpu/$host_os/Make.frag
else
	cpu_os_frag=/dev/null
fi
if test -f $srcdir/config/$host_cpu/Make.frag ; then
	cpu_frag=$srcdir/config/$host_cpu/Make.frag
else
	cpu_frag=/dev/null
fi
AC_SUBST_FILE(cpu_os_frag)dnl
AC_SUBST_FILE(cpu_frag)dnl

dnl =========================================================================

dnl =========================================================================
dnl Allow selection of a specific execution engine
dnl -------------------------------------------------------------------------

AC_ARG_WITH(engine,[  --with-engine=ENGINE    Force use of a specific execution engine])

dnl Translate 'interpreter' to 'intrp'
if test x"$with_engine" = x"interpreter" ; then
	with_engine=intrp
fi

dnl Save the requested engine and set the default to 'jit'
want_engine="$with_engine"
if test x"${with_engine+set}" != x"set" ; then
	with_engine="jit"
fi

dnl -------------------------------------------------------------------------
dnl Check for JIT support and add in the necessary configuration files
dnl -------------------------------------------------------------------------

if test -f $srcdir/config/$host_cpu/$host_os/${with_engine}-md.h ; then
	HAVE_CONFIG_JIT_MD_H=1
	CONFIG_JIT_MD_H="\$(top_srcdir)/config/$host_cpu/$host_os/$with_engine-md.h"
	AC_SUBST(CONFIG_JIT_MD_H)
	KAFFEVM_JIT_DEF="\$(top_srcdir)/config/$host_cpu/$with_engine-$host_cpu.def"
	AC_SUBST(KAFFEVM_JIT_DEF)
	KAFFEVM_ICODE_H="\$(top_srcdir)/config/$host_cpu/$with_engine-icode.h"
	AC_SUBST(KAFFEVM_ICODE_H)
	
	jitsupport=yes
else
	HAVE_CONFIG_JIT_MD_H=0
	with_engine="intrp"
	jitsupport=
fi
AC_SUBST(HAVE_CONFIG_JIT_MD_H)

dnl Make sure we support jit mode if requested.
if test "$jitsupport" = "" && test x"$want_engine" != x"" && test x"$want_engine" != x"intrp" ; then
	AC_MSG_ERROR(Configuration $host_cpu-$host_os does not support $want_engine mode)
fi
AC_MSG_CHECKING(execution engine)
AC_MSG_RESULT($with_engine)
AM_CONDITIONAL(USE_JIT, test x"$with_engine" = x"jit")

ENGINE_NAME="$with_engine"
AC_SUBST(ENGINE_NAME)

AC_SUBST_FILE(engine_frag)dnl
engine_frag=$srcdir/kaffe/kaffevm/$with_engine/Makefile.frag

AC_CHECK_FUNCS(getpagesize)

dnl =========================================================================
dnl Do we include our Sun compatibility scripts?
dnl -------------------------------------------------------------------------

AC_ARG_WITH(suncompat,[  --without-suncompat     Don't include Sun compatibility scripts])
AC_MSG_CHECKING(sun compatibility mode)
if test x"${with_suncompat+set}" != x"set" ; then
	with_suncompat=yes
fi
AM_CONDITIONAL(SUN_COMPAT, test x"$with_suncompat" = x"yes")
AC_MSG_RESULT($with_suncompat)

dnl =========================================================================
dnl Use the new internal threading system "jthreads"
dnl -------------------------------------------------------------------------

AC_ARG_WITH(threads,[  --with-threads=SYSTEM   Define which threading system to use])
AC_MSG_CHECKING(thread system)
if test x"$with_threads" = x"" ; then
	with_threads=unix-jthreads
fi
if test ! -d $srcdir/kaffe/kaffevm/systems/${with_threads} ; then
	AC_MSG_ERROR(Configuration $host_cpu-$host_os does not support $with_threads threads)
fi
THREAD_SYSTEM=$with_threads
AC_MSG_RESULT($THREAD_SYSTEM)
AC_SUBST_FILE(threads_frag)dnl
threads_frag=$srcdir/kaffe/kaffevm/systems/$with_threads/Makefile.frag
AC_SUBST(THREAD_DIR)
THREAD_DIR=systems/$with_threads

dnl =========================================================================
dnl Look for trampoline code if in JIT mode
dnl -------------------------------------------------------------------------
KAFFEVM_TRAMPOLINES_C=
if test "$with_engine" != "intrp" ; then
	AC_MSG_CHECKING(for trampolines)
	if test -f $srcdir/config/$host_cpu/trampolines.c ; then
		KAFFEVM_TRAMPOLINES_C="\$(top_srcdir)/config/$host_cpu/trampolines.c"
		AC_SUBST(KAFFEVM_TRAMPOLINES_C)
		AC_MSG_RESULT(yes)
	else
		AC_MSG_ERROR(does not support trampolines - now mandatory)
	fi
fi

dnl =========================================================================
dnl Set install root directory which prefixes all install target dirs
dnl -------------------------------------------------------------------------

if test x"$with_installroot" != x""; then
    AC_MSG_ERROR([--with-installroot is deprecated, use \`make install DESTDIR=...'])
fi

dnl =========================================================================

dnl =========================================================================
dnl Make the necessary substitutions
dnl -------------------------------------------------------------------------

AC_SUBST(KVER)
AC_SUBST(JAVA_LIBS)
AC_SUBST(with_engine)
AC_SUBST(host_cpu)
AC_SUBST(host_os)
AC_SUBST(THREAD_SYSTEM)
AC_DEFINE_UNQUOTED(KAFFEVERSION, "$KVER", [Kaffe version number])
if test "$program_prefix" = "NONE" ; then
	KPREFIX=""
else
	KPREFIX=$program_prefix
fi
AC_SUBST(KPREFIX)
KAFFE_ARCHOS="$host_cpu-$host_os"
AC_DEFINE_UNQUOTED(ARCHOS, "$KAFFE_ARCHOS", [Define the version we're compiling for])
AC_SUBST(KAFFE_ARCHOS)

dnl =========================================================================

dnl =========================================================================
dnl Checks for programs.
dnl -------------------------------------------------------------------------

AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_CHECK_PROG(JIKES, jikes, jikes)
AC_CHECK_PROG(ZIP, zip, zip)

dnl If symlink is overridden then don't bother with the test.
if test "$LN_S" = "" ; then
	AC_PROG_LN_S
fi
dnl This shell macro caches uses of ln -s in the generated script and
dnl converts them to LN_S
ln() {
	if test "$1" = "-s" ; then
		$LN_S $2 $3
	else
		ln $1 $2
	fi
}

dnl =========================================================================
dnl Checks for types
dnl -------------------------------------------------------------------------

AC_CHECK_SIZEOF(short,0)
AC_CHECK_SIZEOF(int,0)
AC_CHECK_SIZEOF(long,0)
AC_CHECK_SIZEOF(long long,0)
AC_CHECK_SIZEOF(__int64,0)
AC_CHECK_SIZEOF(void*,0)

dnl =========================================================================
dnl Checks for alignments
dnl -------------------------------------------------------------------------

AC_CACHE_CHECK(alignment of void*, ac_cv_alignmentof_voidp,
AC_TRY_RUN([#include <stdio.h>
main() { struct { char c; void *p; } t; FILE *f;
  if ((char*) &t.c != (char*) &t)  exit (1);
  f=fopen ("conftestdata", "w");
  if (! f)                         exit (1);
  fprintf (f, "%d", ((char*) &t.p)-((char*) &t));
  fclose(f); exit (0); }],
ac_cv_alignmentof_voidp=`cat conftestdata`, ac_cv_alignmentof_voidp=1,
ac_cv_alignmentof_voidp=1))
AC_DEFINE_UNQUOTED(ALIGNMENTOF_VOIDP,$ac_cv_alignmentof_voidp, [How are pointers aligned])

dnl -------------------------------------------------------------------------

AC_TYPE_SIGNAL
AC_TYPE_SIZE_T
AC_CHECK_TYPE(ssize_t,int)

dnl -------------------------------------------------------------------------

AC_DEFUN(AC_CHECK_LIBRARY, [dnl
KSAVE_LIBS="$LIBS"
LIBS="$$3 $SAVE_LIBS"
AC_CHECK_LIB($1,$2)
$3=`echo $LIBS | sed "s% $KSAVE_LIBS$%%"`
LIBS="$KSAVE_LIBS"
])

dnl =========================================================================
dnl Checks for libraries
dnl -------------------------------------------------------------------------

dnl Libraries for programs such as Kaffe and kaffeh

KAFFE_LIBS="$KAFFE_LIBS"
AC_SUBST(KAFFE_LIBS)

dnl Checks for general math libraries

AC_CHECK_LIBRARY(mw,_mwvalidcheckl,M_LIBS)
AC_CHECK_LIBRARY(m,sin,M_LIBS)

AC_SUBST(M_LIBS)

dnl Checks for libraries for vm library.
dnl Include work around FreeBSD modulo bug.

case "$host_os" in
freebsd2|netbsd1)
	AC_CHECK_LIBRARY(gcc,__moddi3,VM_LIBS);;
esac

AC_SUBST(VM_LIBS)

dnl Check for libraries for native library.
dnl None required.

dnl Check for libraries for net library.
AC_CHECK_FUNC(gethostname)
if test $ac_cv_func_gethostname = no; then
      AC_CHECK_LIBRARY(nsl, gethostname, NET_LIBS)
fi
AC_CHECK_FUNC(gethostbyname)
if test $ac_cv_func_gethostbyname = no; then
      AC_CHECK_LIBRARY(nsl, gethostbyname, NET_LIBS)
fi
AC_CHECK_FUNC(connect)
if test $ac_cv_func_connect = no; then
  AC_CHECK_LIBRARY(socket, connect, NET_LIBS)
fi
AC_SUBST(NET_LIBS)

dnl Check for libraries for zip library.
AC_CHECK_LIBRARY(z,deflate,ZIP_LIBS)
AC_SUBST(ZIP_LIBS)

dnl Check for libraries for math library.
AC_CHECK_LIBRARY(gmp,mpz_add,MATH_LIBS)
AC_SUBST(MATH_LIBS)

dnl Checks for libraries for X related libraries.
AC_PATH_XTRA
if test x"$no_x" = x"yes"; then
  AWT_LIBS=
else
  OLIBS=$LIBS
  LIBS="$ZIP_LIBS $X_LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS $LIBS"
  AC_CHECK_LIBRARY(jpeg,jpeg_read_header,JPEG_LIBS)
  AC_CHECK_LIBRARY(png,png_create_info_struct,PNG_LIBS)
  AC_CHECK_LIBRARY(gif,DGifOpenFileHandle,GIF_LIBS)
  LIBS=$OLIBS
  dnl If libpng is included, we also need libz - if we don't have it then
  dnl we can't include png support.
  if test "$PNG_LIBS" != "" ; then
	if test "$ZIP_LIBS" != "" ; then
		PNG_LIBS="$PNG_LIBS $ZIP_LIBS"
	else
		PNG_LIBS=""
	fi
  fi
  dnl -R in X_PRE_LIBS should only be replaced with -rpath here,
  dnl otherwise configure tests that don't use libtool will fail
  AWT_LIBS="$JPEG_LIBS $PNG_LIBS $GIF_LIBS $X_LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS"
  AC_SUBST(AWT_LIBS)
fi

dnl -------------------------------------------------------------------------

dnl =========================================================================
dnl Checks for header files.
dnl -------------------------------------------------------------------------

CPPFLAGS="$CPPFLAGS -I/usr/local/include"
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h sys/socket.h winnt.h wintypes.h wtypes.h winbase.h windows.h winsock.h dlfcn.h sys/ioctl.h sys/filio.h malloc.h alloca.h sys/param.h sys/utsname.h pwd.h asm/sigcontext.h sigcontext.h asm/signal.h signal.h mach-o/rld.h sys/types.h sys/stat.h string.h sys/select.h memory.h bsd/libc.h dl.h features.h sys/wait.h zlib.h netinet/tcp.h netinet/in_systm.h netinet/in.h netdb.h jpeglib.h gif_lib.h png.h gmp.h)
AC_HEADER_DIRENT

dnl We need both getrusage and the header containing its decls.
have_rusage=no
AC_CHECK_HEADERS(sys/resource.h, AC_CHECK_FUNCS(getrusage, have_rusage=yes))

dnl =========================================================================
dnl Do we build a timing measuring vm?
dnl -------------------------------------------------------------------------

AC_ARG_WITH(timing,[  --with-timing           Time vm execution])

if test "$with_timing" = "yes" ; then
	if test $have_rusage = "no" ; then
		echo "Can't build with timing but no rusage"
	else
		AC_DEFINE(TIMING, 1, [Do we time various pieces of the vm?])
	fi
fi

dnl -------------------------------------------------------------------------

dnl =========================================================================
dnl Checks for typedefs, structures, and compiler characteristics.
dnl -------------------------------------------------------------------------

AC_HEADER_TIME
AC_STRUCT_TM
AC_STRUCT_TIMEZONE
AC_C_BIGENDIAN
AC_C_CHAR_UNSIGNED
AC_C_INLINE

dnl =========================================================================
dnl Check for struct sigcontext.
dnl -------------------------------------------------------------------------

AC_CACHE_CHECK([for struct sigcontext],
  ac_cv_struct_sigcontext,
[AC_TRY_COMPILE([#include <asm/signal.h>
#include <asm/sigcontext.h>], 
[struct sigcontext t;],
  ac_cv_struct_sigcontext=yes, ac_cv_struct_sigcontext=no)])
if test $ac_cv_struct_sigcontext = yes; then
  AC_DEFINE(HAVE_STRUCT_SIGCONTEXT, 1, [Do we have sigcontext])
fi

dnl Check for struct sigcontext_struct.
dnl Just to complicate things there are two different sets of assembly files
dnl we must check which are mutually incompatible.

AC_CACHE_CHECK([for struct sigcontext_struct (without asm/sigcontext.h)],
  ac_cv_struct_sigcontext_struct,
[AC_TRY_COMPILE([#include <asm/signal.h>],
[struct sigcontext_struct t;],
  ac_cv_struct_sigcontext_struct=yes, ac_cv_struct_sigcontext_struct=no)])

AC_CACHE_CHECK([for struct sigcontext_struct (with asm/sigcontext.h)],
  ac_cv_struct_sigcontext_struct_with,
[AC_TRY_COMPILE([#include <asm/signal.h>
#include <asm/sigcontext.h>], 
[struct sigcontext_struct t;],
  ac_cv_struct_sigcontext_struct_with=yes, ac_cv_struct_sigcontext_struct_with=no)])

if test $ac_cv_struct_sigcontext_struct = yes -o $ac_cv_struct_sigcontext_struct_with = yes; then
  AC_DEFINE(HAVE_STRUCT_SIGCONTEXT_STRUCT, 1, [Do we have sigcontext_struct])
fi

dnl -------------------------------------------------------------------------

dnl =========================================================================
dnl Checks for library functions.
dnl -------------------------------------------------------------------------

AC_FUNC_STRFTIME
AC_FUNC_MMAP
AC_CHECK_FUNCS(select socket getsockname)
AC_CHECK_FUNCS(memcpy memmove)
AC_CHECK_FUNCS(mkdir)
AC_CHECK_FUNCS(getcwd getwd gettimeofday ftime time uname getuid)

KSAVE_LIBS="$LIBS"
LIBS="$M_LIBS $LIBS"
AC_CHECK_FUNCS(remainder remainderf fmod fmodf drem rint floor ceil finite isinf isnan strtod)
LIBS="$KSAVE_LIBS"

AC_CHECK_FUNCS(strerror hstrerror)
AC_CHECK_FUNCS(fcntl ioctl)
AC_CHECK_FUNCS(alarm setitimer)
AC_CHECK_FUNCS(sigprocmask sigsetmask sigemptyset sigaddset signal sigaction)
AC_CHECK_FUNCS(sbrk valloc memalign mallopt)
AC_CHECK_FUNCS(mprotect madvise)
AC_CHECK_FUNCS(waitpid kill fork execve execvp)
AC_CHECK_FUNCS(sync fsync)
AC_CHECK_FUNCS(atexit on_exit vsnprintf snprintf)

if test "$host_cpu" = "alpha" ; then
  AC_CACHE_CHECK([for alpha support of amask instruction],
    ac_cv_alpha_asm_amask,
  [AC_TRY_COMPILE(, [ long r; __asm__("amask 1,%0" : "=r"(r)); return !r; ],
    ac_cv_alpha_asm_amask=yes, ac_cv_alpha_asm_amask=no)
  ])
  if test "$ac_cv_alpha_asm_amask" = "yes" ; then
    AC_DEFINE(HAVE_ALPHA_ASM_AMASK, 1, [Do we have amask instruction on alpha])
  fi
fi

dnl -------------------------------------------------------------------------

dnl =========================================================================
dnl Work out if select is defined
dnl -------------------------------------------------------------------------

AC_CACHE_CHECK([for declaration of select],
  ac_cv_declared_select,
[AC_TRY_COMPILE([
#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif
#ifdef HAVE_SYS_TIME_H
#include <sys/time.h>
#endif
#ifdef HAVE_SYS_SELECT_H
#include <sys/select.h>
#endif
], [ void *foo = select; ],
  ac_cv_declared_select=yes; AC_DEFINE(HAVE_DECLARED_SELECT, 1, [Is function select declared]), ac_cv_declared_select=no)
])

dnl -------------------------------------------------------------------------

dnl If we have X then include AWT support.
if test "$have_x" = "yes" ; then
  AWT_DIR=X
fi

if test x"${AWT_DIR+set}" = x"set"; then
  AWT_LIB="\$(top_builddir)/libraries/clib/awt/$AWT_DIR/libawt.la"
else
  AWT_DIR=
  AWT_LIB=
fi

AC_SUBST(AWT_DIR)

JAVA_LIBS=" \
\$(top_builddir)/libraries/clib/native/libnative.la \
\$(top_builddir)/libraries/clib/net/libnet.la \
\$(top_builddir)/libraries/clib/io/libio.la \
\$(top_builddir)/libraries/clib/zip/libzip.la \
\$(top_builddir)/libraries/clib/math/libmath.la \
\$(top_builddir)/libraries/clib/management/libmanagement.la \
$AWT_LIB \
$JAVA_LIBS \
"

DLOPEN_JAVA_LIBS=
for lib in $JAVA_LIBS; do
  DLOPEN_JAVA_LIBS="$DLOPEN_JAVA_LIBS -dlopen $lib"
done
AC_SUBST(DLOPEN_JAVA_LIBS)

if test "$dynamic_libraries" = no; then
	KLIBFLAGS=-static
else
	KLIBFLAGS=
fi
AC_SUBST(KLIBFLAGS)

AC_SUBST(KLIBFLAGS)
if test "$vm_dynamic_library" = no; then
	KVMLIBFLAGS=-static
else
	KVMLIBFLAGS=
fi
AC_SUBST(KVMLIBFLAGS)

dnl =========================================================================
dnl Set up for local build of kaffeh when cross-compiling
dnl -------------------------------------------------------------------------

if test "$cross_compiling" = yes; then
  dnl check for variables that, if not defined in config.frag or config.cache,
  dnl may take wrong values
  if test x"${ac_cv_c_bigendian+set}" != x"set"; then
    AC_MSG_WARN([when cross compiling, you may want to set ac_cv_c_bigendian to yes or no])
  fi
  if test x"${ac_cv_c_char_unsigned}" != x"set"; then
    AC_MSG_WARN([when cross compiling, you may want to set ac_cv_c_char_unsigned to yes or no])
  fi
  dnl force user to set BUILD_CC
  if test x"${BUILD_CC+set}" != x"set" &&
     test x"${ac_cv_BUILD_CC}" != x"set"; then
    AC_MSG_ERROR([when cross compiling, BUILD_CC should be set to the local C compiler])
  fi
  ac_cv_BUILD_CC="${BUILD_CC-${ac_cv_BUILD_CC}}"
  BUILD_CC="$ac_cv_BUILD_CC"
  KAFFEH_FOR_BUILD="\$(top_builddir)/kaffe/kaffeh-build/kaffeh\$(EXEEXT)"
  MAKE_KAFFEH_FOR_BUILD="cd \$(top_builddir)/kaffe/kaffeh && \$(MAKE) kaffeh-build"
else
  BUILD_CC="$CC" dnl just in case
  KAFFEH_FOR_BUILD="\$(top_builddir)/kaffe/kaffeh/kaffeh\$(EXEEXT)"
  MAKE_KAFFEH_FOR_BUILD="cd \$(top_builddir)/kaffe/kaffeh && \$(MAKE)"
fi

AC_SUBST(BUILD_CC)
AC_SUBST(KAFFEH_FOR_BUILD)
AC_SUBST(MAKE_KAFFEH_FOR_BUILD)

dnl =========================================================================
dnl Define directory to put native library support
dnl -------------------------------------------------------------------------

# We do this hear to because we need prefixes set.
test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix='${prefix}'
nativedir=$libdir/kaffe
AC_SUBST(nativedir)

dnl =========================================================================
dnl Define kaffehome.
dnl -------------------------------------------------------------------------

classdir=`eval echo $datadir`/kaffe
AC_DEFINE_UNQUOTED(DEFAULT_KAFFEHOME, "$classdir", [Define default KAFFEHOME])
AC_SUBST(classdir)

dnl -------------------------------------------------------------------------

dnl =========================================================================
dnl Extract path and dir separator information from paths.h
dnl -------------------------------------------------------------------------

AC_PROG_CPP

AC_CACHE_CHECK([for path separator],ac_cv_kaffe_pathsep,
[	rm -f conftest.c
	cat ${srcdir}/include/system.h > conftest.c
	echo path_separator >> conftest.c
	eval ac_cv_kaffe_pathsep=`$CPP $CPPFLAGS conftest.c | sed '$p; d'`
	rm -f conftest.c
])
PATHSEP=$ac_cv_kaffe_pathsep
AC_SUBST(PATHSEP)

AC_CACHE_CHECK([for directory separator],ac_cv_kaffe_dirsep, 
[	rm -f conftest.c
	cat ${srcdir}/include/system.h > conftest.c
	echo file_separator >> conftest.c
	eval ac_cv_kaffe_dirsep=`$CPP $CPPFLAGS conftest.c | sed '$p; d'`
	rm -f conftest.c
])
DIRSEP=$ac_cv_kaffe_dirsep
AC_SUBST(DIRSEP)

dnl These are not to be used during configuration, only during build.
CPPFLAGS="$CPPFLAGS -DKVER='\"\$(KVER)\"' -I\$(top_srcdir)/config -I\$(top_srcdir)/include"

AC_CONFIG_SUBDIRS(libltdl)
AM_CONFIG_HEADER(config/config.h include/jtypes.h)
AC_OUTPUT(Makefile \
config/Makefile include/Makefile kaffe/Makefile 
libraries/Makefile kaffe/kaffevm/Makefile \
kaffe/kaffevm/intrp/Makefile kaffe/kaffevm/jit/Makefile \
kaffe/kaffevm/systems/Makefile \
kaffe/kaffevm/systems/unix-jthreads/Makefile \
kaffe/kaffevm/systems/oskit-pthreads/Makefile \
kaffe/kaffevm/systems/linux-threads/Makefile \
kaffe/kaffevm/systems/beos-native/Makefile \
kaffe/kaffe/Makefile kaffe/kaffeh/Makefile \
kaffe/man/Makefile kaffe/scripts/Makefile \
kaffe/scripts/bat/Makefile kaffe/scripts/compat/Makefile \
libraries/javalib/Makefile \
libraries/clib/Makefile \
libraries/clib/native/Makefile \
libraries/clib/io/Makefile \
libraries/clib/net/Makefile \
libraries/clib/zip/Makefile \
libraries/clib/math/Makefile \
libraries/clib/management/Makefile \
libraries/clib/awt/Makefile \
libraries/clib/awt/X/Makefile \
kaffe/man/kaffe.1 \
libraries/javalib/rebuildLib \
kaffe/scripts/install-jar \
kaffe/scripts/kaffe \
kaffe/scripts/javac \
kaffe/scripts/javakey \
kaffe/scripts/jar \
kaffe/scripts/javap \
kaffe/scripts/jdb \
kaffe/scripts/pizza \
kaffe/scripts/javadoc \
kaffe/scripts/pizzadoc \
kaffe/scripts/appletviewer \
kaffe/scripts/rmic \
kaffe/scripts/rmiregistry \
kaffe/scripts/serialver \
kaffe/scripts/native2ascii \
kaffe/scripts/compat/java \
kaffe/scripts/compat/javac \
kaffe/scripts/compat/appletviewer \
kaffe/scripts/compat/javadoc \
test/Makefile \
test/regression/Makefile \
test/regression/TestScript \
test/regression/TestScriptThread \
)
